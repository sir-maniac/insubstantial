apply plugin: 'java'
apply plugin: 'maven'

group = "com.github.insubstantial"
version = "6.2-SNAPSHOT"
versionKey = "6.2-invincible_SNAPSHOT"
release = "internal"
timeStamp = new Date().format("MMMM d, yyyy HH:mm:ss z")

sourceCompatibility = 1.6
targetCompatibility = 1.6

task wrapper(type: Wrapper) {
  gradleVersion = '0.9.2'
}

repositories {
  mavenRepo name: 'sonatype staging', urls: 'https://oss.sonatype.org/content/groups/staging'
  mavenCentral()
  mavenRepo urls: new File(System.getProperty('user.home'), '.m2/repository').toURI().toString()
}

dependencies {
  compile group:'com.github.insubstantial', name:'trident', version:version
  compile group:'asm', name:'asm-all', version:'2.2.3'
  compile group:'org.apache.ant', name:'ant', version:'1.7.0'
}

jar {
  manifest {
    attributes(
      "Laf-Widget-Version": version,
      "Laf-Widget-VersionName": versionKey,
      "Laf-Widget-BuildStamp": timeStamp
    )
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.java
  from sourceSets.main.resources
  classifier = 'src'
}

task javadocJar(type: Jar) {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

artifacts {
  archives sourceJar
  archives javadocJar
}

uploadArchives {
  try {
    def x = [deployUsername, deployPassword]
  } catch (Exception e) {
    deployUsername = 'unset'
    deployPassword = ''
  }
  repositories {
    mavenDeployer {
      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication userName: deployUsername, password: deployPassword
      }
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication userName: deployUsername, password: deployPassword
      }
      pom.project {
        modelVersion '4.0.0'
        packaging 'jar'
        name "plaf widget"
        description "A fork of @kirilcool's laf-widget project"
        url "http://insubstantial.github.com/plafwidget"
        licenses {
          license {
            name 'The BSD License'
            url 'http://www.opensource.org/licenses/bsd-license.php'
            distribution 'repo'
            comments 'Covers ASM, BlogOfBug, JibX, LafWidget, and Looks contributions'
          }
          license {
            name 'CC BY-SA 2.5'
            url 'http://creativecommons.org/licenses/by-sa/2.5/'
            distribution 'repo'
            comments 'Covers the TangoIcons'
          }
        }
        scm {
          connection 'scm:git:git@github.com:Insubstantial/plafwidget.git'
          developerConnection 'scm:git:git@github.com:Insubstantial/plafwidget.git'
          url 'scm:git:git@github.com:Insubstantial/plafwidget.git'
        }
        developers {
          developer {
            name 'Kirill Grouchnikov'
            email 'kirillcool@yahoo.com'
            roles {
              role 'author'
              role 'developer'
            }
          }
          developer {
            name 'Danno Ferrin'
            email 'danno.ferrin@shemnon.com'
            roles {
              role 'maintainer'
            }
          }
        }
      }
    }
  }
}